set createInheritanceSkillLinkedList 0x009D1C00
set viewHandler 0x009D1ED0
set msgHandler 0x009D2340
set inheritanceMenuCase 0x009D2400
set clearViewHandlerGlobals 0x009D2580
set force_O_ButtonPress 0x009D2600
set finalStepsBeforeFusion 0x009D2670

write32 0x007BB8D4 0x009D2400 // Add case 9 for inheritance menu

// View persona draw structure -> 009D1E90
// Inheritance ended var -> 009D1EA4
// Result persona original skill number -> 009D1EA0
// Choosen inherited skills list -> 009D1EB0

// Inheritance skills linked list -> 009D1EC0
// Number of inheritable skills -> 009D1EC4
// Selected inheritable skill index -> 009D1EC8
// Current final inherited skill list index -> 009D1ECC


// ARGUMENTS ------------------------|
// a0 -> linked list info            |
ASM_START clearViewHandlerGlobals
    addiu $sp,$sp,-0x10
    sd $ra,0x0($sp)

    jal 0x003c45f0 // Free linked list
    nop

    lui $a1, 0x009d

    sw $zero, 0x1EC0($a1) // Clear former skill linked list
    sw $zero, 0x1ECC($a1) // Reset skill list index
    sw $zero, 0x1EC8($a1) // Reset current selected element from linked list

    addu $a0, $a1, 0x1EB0
    daddu $a1, $zero, $zero
    li $a2, 0x10
    jal 0x00521408 // memset to 0 the global values we used for next fusion
    nop

    ld $ra,0x0($sp)
    addiu $sp,$sp,0x10
    jr $ra
ASM_END

ASM_START viewHandler
    addiu $sp,$sp,-0xB0
    sd $ra,0x90($sp)
    sd $fp,0x80($sp)
    sd $s7,0x70($sp)
    sd $s6,0x60($sp)
    sd $s5,0x50($sp)
    sd $s4,0x40($sp)
    sd $s3,0x30($sp)
    sd $s2,0x20($sp)
    sd $s1,0x10($sp)
    sd $s0,0x0($sp)

    move $s5, $a3
    sub $s3, $s1, 0x74 // Save fusion structure

    lui $s7, 0x009d
    lw $v0, 0x1EC0($s7) // Load inheritable skills linked list address
    bne $v0, $zero, listAlreadyCreated
    nop
        addiu $a0, $s3, 0x20 // a0 -> persona_result_info in fusion structure
        lw $a1, 0x18($s3) // a1 -> array_of_personas_in_fusion
        lw $a2, 0xc($s3) // a2 -> number of members of fusion
        jal 0x009D1C00 // Create linked list with all inheritable skills
        nop
        sw $v0, 0x1EC0($s7)
        sw $v1, 0x1EC4($s7)
        sw $a0, 0x1EA0($s7)

    listAlreadyCreated: lw $s0, 0x1EC0($s7) // New linked list info
    lw $s1, 0x1EC4($s7) // Number of inheritable skills
    lw $s2, 0x1EC8($s7) // Selected inheritable skill index

    bne $s5, $zero, o_pressed // Special case when cancelling final dialogue before fusion
    nop

    lui $s6, 0x007e // Check X press
    lhu $v0, 0x094e($s6)
    andi $v0, $v0, 0x40
    beq $v0, $zero, x_not_pressed
    nop
    lhu $v0, 0x0958($s6)
    andi $v0, $v0, 0x40
    bne $v0, $zero, x_pressed
    nop

    x_not_pressed: lui $s6, 0x007e // Check O press
    lhu $v0, 0x094e($s6)
    andi $v0, $v0, 0x20
    beq $v0, $zero, o_not_pressed
    nop
    lhu $v0, 0x0958($s6)
    andi $v0, $v0, 0x20
    bne $v0, $zero, o_pressed
    nop
    
    o_not_pressed: lhu $v0, 0x094e($s6) // Check down press
    andi $v0, $v0, 0x4000
    bne $v0, $zero, down_pressed
    nop

    down_not_pressed: lhu $v0, 0x094e($s6) // Check up press
    andi $v0, $v0, 0x1000
    beq $v0, $zero, functionEnd
    nop

    up_pressed: addiu $v0, $zero, 0x1
    sub $s2, $s2, $v0
    bgez $s2, final_jump
    nop
    lw $v1, 0x10($s0)
    sub $s2, $v1, $v0
    final_jump: beq $zero, $zero, update_index
    nop

    down_pressed: addiu $v0, $zero, 0x1
    add $s2, $s2, $v0

    update_index: daddu $v0, $zero, $s2
    lw $v1, 0x10($s0)
    div $v0, $v1
    mfhi $v0
    sw $v0, 0x1EC8($s7)
    beq $zero, $zero, functionEnd
    nop

    o_pressed: lw $a1, 0x1ECC($s7)
    li $v1, 0x1
    sub $a1, $a1, $v1
    bltz $a1, backFromInheritance
    nop

    sll $v0, $a1, $v1
    addiu $v1, $s7, 0x1EB0
    addu $v0, $v0, $v1 
    lh $a3, 0x0($v0) // Save cancelled skill for later
    sh $zero, 0x0($v0) // Reset current fusion result skill

    li $v1, 0x1
    sll $v0, $a1, $v1
    lw $v1, 0x1E90($s7)
    addu $v1, $v1, $v0
    addiu $a0, $v1, 0x78
    lw $v0, 0x1EA0($s7)
    sub $v0, $v0, $s1
    li $v1, 0x1
    sll $v0, $v0, $v1
    addu $v0, $a0, $v0
    addu $v1, $zero, 0xfff
    sh $v1, 0x0($v0) // Reset draw result skills

    sw $a1, 0x1ECC($s7) // Update total number of skills selected


    daddu $a0,$s0,$zero // $a0 -> inherited linked list information
    addiu $a1,$s0,0x4 // $a1 -> linked list first element
    daddu $a2,$s0,$zero // $a2 -> value to find, in this case current_skill
    lui $a2, 0x0001
    or $a2, $a2, $a3
    jal 0x003C4BF0 // Returns linked list element equal to value, if not found returns void linked list element
    nop
    beq $v0, $zero, functionEnd // If not found we jump to function end
    nop

    sh $zero, 0x6($v0) // Clear selected skill flag

    li $v1, 0x0
    lw $a0, 0x4($a0)
    beq $zero, $zero, clearForEnd
    nop
    clearForStart: bne $v0, $a0, clearForCondition
    nop

    sw $v1, 0x1EC8($s7) // Bring selection index back to the just cancelled skill

    beq $zero, $zero, functionEnd
    nop

    clearForCondition: lw $a0, 0x10($a0)
    addiu $v1, $v1, 0x1
    clearForEnd: bne $a0, $zero, clearForStart
    nop


    beq $zero, $zero, functionEnd
    nop

    x_pressed: lw $v0, 0x10($s0) // If there are no elements in the linked list we don't continue
    beq $v0, $zero, functionEnd
    nop
    lw $s6,0x4($s0)
    li $s5, 0x0
    beq $zero,$zero,searchIndexForEnd // For loop to find current index element
    nop
    searchIndexForStart: bne $s5, $s2, searchIndexForCondition // If index not found look next one
    nop

    lh $v0, 0x6($s6) 
    bne $v0, $zero, functionEnd // If skill already selected we don't add this skill
    nop

    li $v0, 0x1
    sh $v0, 0x6($s6) // Store this element has been already selected

    lh $s4, 0x4($s6) // Load current skill

    lw $v0, 0x1ECC($s7)
    li $v1, 0x1
    sll $v0, $v0, $v1
    addiu $v1, $s7, 0x1EB0
    addu $v0, $v0, $v1 // Get current list position to store chosen skill
    sh $s4, 0x0($v0)

    lw $v0, 0x1ECC($s7) // Update draw view
    li $v1, 0x1
    sll $v0, $v0, $v1
    lw $v1, 0x1E90($s7)
    addu $v1, $v1, $v0
    addiu $a0, $v1, 0x78 // Add current skill index

    lw $v0, 0x1EA0($s7)
    sub $v0, $v0, $s1
    li $v1, 0x1
    sll $v0, $v0, $v1
    addu $v0, $a0, $v0 // Add original skills offset

    sh $s4, 0x0($v0) // Store value of skill in view

    lw $v0, 0x1ECC($s7) // Update skill index
    li $v1, 0x1
    addu $v0, $v0, $v1
    sw $v0, 0x1ECC($s7)

    bne $s1, $v0, functionEnd // If we still need to select more skills we jump to function end
    nop

    li $v0, 0x1 // Continue with fusion
    sw $v0, 0x1EA4($s7)

    beq $zero,$zero,proceedToFusion
    nop 
    searchIndexForCondition: addiu $s5,$s5,0x1
    lw $s6,0x10($s6)
    searchIndexForEnd: bne $s6,$zero,searchIndexForStart
    nop
    beq $zero, $zero, functionEnd
    nop

    backFromInheritance: addiu $v0, $s3, 0x74
    li $v1, 0x4
    sw $v1, 0x4($v0)

    addu $a0, $s0, $zero
    jal 0x009D2580 // Clear globals used for the handler
    nop

    functionEnd: li $v0, 0x0

    proceedToFusion: nop

    ld $ra,0x90($sp)
    ld $fp,0x80($sp)
    ld $s7,0x70($sp)
    ld $s6,0x60($sp)
    ld $s5,0x50($sp)
    ld $s4,0x40($sp)
    ld $s3,0x30($sp)
    ld $s2,0x20($sp)
    ld $s1,0x10($sp)
    ld $s0,0x0($sp)
    addiu $sp,$sp,0xB0
    jr $ra
ASM_END

ASM_START finalStepsBeforeFusion
    lui $a3, 0x009d
    lw $a2, 0x1ecc($a3)
    sub $a1, $s1, 0x74 // Save fusion structure

    addiu $a0, $a1, 0x2c // Get result persona skills address
    lw $v0, 0x1EA0($a3)
    sub $v0, $v0, $a2
    li $v1, 0x1
    sll $v0, $v0, $v1
    addu $a0, $a0, $v0
    sll $a2, $a2, $v1
    addu $a1, $a3, 0x1EB0
    jal 0x00521250 // memcpy our skill list onto the result persona list
    nop

    lw $a0, 0x1EC0($a3) // Clear globals
    jal 0x009D2580
    nop
    
    j 0x003DDFBC
    nop
ASM_END

ASM_START 0x003ddfa0
    j 0x009D2670 // We clear globals when going to the actual fusion
    nop
ASM_END

ASM_START force_O_ButtonPress
    li $a3, 0x1
    jal 0x009D1ED0 // We force back button press to erase last skill
    nop
    j 0x003DDFBC
    nop
ASM_END

ASM_START 0x003ddfa8
    addiu $v0, $zero, 0x9
    sw $v0, 0x4($s1)
    j 0x009D2600
    nop
ASM_END

ASM_START 0x003ddcc0
    jal 0x0016deb0
    lui $s7, 0x009d
    dsll32 $s1, $v0, 0x10
    dsra32 $s1, $s1, 0x10
    daddu $a0, $s1, $zero
    jal 0x0016dba0
    sw $s0, 0x1E90($s7) // Save draw structure for updating the view
ASM_END

ASM_START inheritanceMenuCase
    jal 0x009D1ED0
    li $a3, 0x0
    beq $v0, $zero, inheritanceNotEnded
    nop
    li $v0, 0x7
    sw $v0, 0x4($s1)
    inheritanceNotEnded: j 0x003ddfbc
    nop
ASM_END

ASM_START 0x003dde84
    lui $v0, 0x009D
    lw $v1, 0x1EA4($v0)
    bne $v1,$zero,beginFusion
    nop 
    lw $v0,0x0($s1)
    ori $v0,$v0,0x10
    sw $v0,0x0($s1)
    addiu $a0,$zero,0x2
    jal 0x003C7430
    nop 
    beq $zero,$zero,beginMsgHandlerCase
    nop 
    beginFusion: lhu $a0,0x1E($s1)
    jal 0x003D5DC0
    sw $zero, 0x1EA4($v0)
    daddu $a0,$v0,$zero
    jal 0x003C7430
    nop 
    daddu $a0,$zero,$zero
    jal 0x003C74E0
    nop 
    beginMsgHandlerCase: addiu $v0,$zero,0x8
    sw $v0,0x4($s1)
ASM_END

ASM_START msgHandler
    andi $v0, $v1, 0x10
    beq $v0, $zero, msgOptionsHandler
    nop
    li $v0, -0x11
    and $v0, $v1, $v0
    sw $v0, 0x0($s1)
    li $v0, 0x9
    sw $v0, 0x4($s1)
    j 0x003ddfbc
    nop

    msgOptionsHandler: jal 0x003c7610
    nop
    j 0x003ddf84
    nop
ASM_END

ASM_START 0x003ddf7c
    j 0x009D2340
    nop
ASM_END

ASM_START 0x003DDD3C
    sltiu $at, $v0, 0x10 // Add case 9 for inheritance menu
ASM_END

// ARGUMENTS ------------------------|
// a0 -> persona_result_info         |
// a1 -> array_of_personas_in_fusion |
// a2 -> number of members of fusion |
// RETURNS ------------------------- |
// v0 -> skill linked list           |
// v1 -> number of inherited skills  |
// a0 -> number of persona skills    |
ASM_START createInheritanceSkillLinkedList
    addiu $sp,$sp,-0xB0
    sd $ra,0x90($sp)
    sd $fp,0x80($sp)
    sd $s7,0x70($sp)
    sd $s6,0x60($sp)
    sd $s5,0x50($sp)
    sd $s4,0x40($sp)
    sd $s3,0x30($sp)
    sd $s2,0x20($sp)
    sd $s1,0x10($sp)
    sd $s0,0x0($sp)

    daddu $s4,$a0,$zero // persona_result_info
    daddu $s6,$a1,$zero // array_of_personas_in_fusion
    daddu $s7,$a2,$zero // members_of_fusion

    daddu $s2,$zero,$zero
    daddu $a0,$zero,$zero
    daddu $a1,$zero,$zero
    daddu $a2,$zero,$zero
    daddu $a3,$zero,$zero
    jal 0x003c44d0 // Create linked list info for all possible inherited skills
    nop

    daddu $s1,$v0,$zero
    daddu $s3,$zero,$zero
    beq $zero,$zero,personaForLoopCondition
    nop 

    personaForLoopStart: daddu $s5,$zero,$zero
    sll $v0,$s3,0x02
    addu $v0,$s6,$v0 // Get persona in list
    sd $v0,0xA0($sp) 
    beq $zero,$zero,skillForLoopCondition
    nop
    
    skillForLoopStart: sll $v1,$s5,0x01
    ld $v0,0xA0($sp)
    lw $v0,0x0($v0)
    addu $v0,$v0,$v1
    lhu $s0,0xC($v0) // Load current persona skill
    beq $s0,$zero,nextSkillIteration // If skill is null we jump to next skill iteration
    nop 

    addiu $s2,$s2,0x1 // Increment iterations counter
    daddu $a0,$s4,$zero // $a0 -> persona_result_info
    daddu $a1,$s0,$zero // $a1 -> current_skill
    jal 0x00176990 // Get index of skill, if it doesn't exist return -1
    nop
    addiu $v1,$zero,-0x1
    bne $v0,$v1,nextSkillIteration  // If skill is present in result persona we jump to next skill iteration
    nop 

    daddu $a0,$s1,$zero // $a0 -> inherited linked list information
    addiu $a1,$s1,0x4 // $a1 -> linked list first element
    daddu $a2,$s0,$zero // $a2 -> value to find, in this case current_skill
    jal 0x003C4BF0 // Returns linked list element equal to value, if not found returns void linked list element
    nop 
    bne $v0,$zero,nextSkillIteration // If current_skill already in the list we jump to next skill iteration
    nop 

    sll $v1,$s0,0x01 // Whole inheritance check of current_skill (black box, checks a bunch of static data in the code)
    lw $v0,-0x48FC($gp)
    addu $v0,$v0,$v1
    lb $v0,0x0($v0)
    addiu $v1,$zero,-0x1
    bne $v0,$v1,endIf_1
    nop 
    addiu $v0,$zero,0x12
    endIf_1: lhu $a0,0x2($s4)
    sll $v1,$a0,0x03
    sub $v1,$v1,$a0 // Should work, but originally was a "subu"
    sll $a0,$v1,0x01
    lw $v1,-0x48D0($gp)
    addu $v1,$v1,$a0
    lhu $a0,0xA($v1)
    sll $v1,$a0,0x03
    addu $v1,$v1,$a0
    sll $v1,$v1,0x01
    addu $v1,$v1,$a0
    sll $a0,$v1,0x01
    lui $v1,0x006A
    addiu $v1,$v1,0x48F0
    addu $v1,$v1,$a0
    sll $v0,$v0,0x01
    addu $v0,$v0,$v1
    lh $fp,0x0($v0)
    jal 0x0017D800
    nop 
    beq $v0,$zero,ifStatement_2
    nop 
    lui $v0,0x006A
    addiu $v0,$v0,0x4E10
    addu $v0,$v0,$s0
    lb $v1,0x0($v0)
    beq $zero,$zero,endIf_2
    nop 
    ifStatement_2: lui $v0,0x006A
    addiu $v0,$v0,0x4BA0
    addu $v0,$v0,$s0
    lb $v1,0x0($v0)
    endIf_2: bne $v1,$zero,ifStatement_3
    nop 
    daddu $v0,$zero,$zero
    beq $zero,$zero,endIf_3
    nop 
    ifStatement_3: sll $v0,$fp,0x02
    addu $v0,$v0,$fp
    sll $v0,$v0,0x01
    div $v0,$v1
    mflo $v0
    endIf_3: beq $v0,$zero,nextSkillIteration
    nop 
    ifSkillInheritable: daddu $a0,$s1,$zero // $a0 -> inherited linked list information
    daddu $a1,$zero,$zero // $a1 -> new linked list element to add, in this case 0 will create a new element
    daddu $a2,$zero,$zero // $a2 -> size of linked list element
    jal 0x003C4910 // Adds a new element into an existing linked list
    nop 
    sw $s0,0x4($v0) // Store current_skill value in new added element

    nextSkillIteration: addiu $s5,$s5,0x1
    skillForLoopCondition: slti $v0,$s5,0x0008
    bne $v0,$zero,skillForLoopStart
    nop 

    addiu $s3,$s3,0x1
    personaForLoopCondition: slt $v0,$s3,$s7
    bne $v0,$zero,personaForLoopStart


    addiu $a1,$zero,0x8 // Get number of inherited skills result persona will have
    lui $v1,0x006A
    addiu $v1,$v1,0x48D0
    beq $zero,$zero,inheritableSkillNumberForLoopEnd
    nop 
    inheritableSkillNumberForLoopStart: sll $a0,$a1,0x01
    addu $v0,$v1,$a0
    lb $v0,0x0($v0)
    slt $at,$s2,$v0
    bne $at,$zero,inheritableSkillNumberForLoopCondition
    nop 
    lui $v0,0x006A
    addiu $v0,$v0,0x48D1
    addu $v0,$v0,$a0
    lb $s0,0x0($v0)
    beq $zero,$zero,inheritableSkillNumberOut
    nop 
    inheritableSkillNumberForLoopCondition: addiu $a1,$a1,-0x1
    inheritableSkillNumberForLoopEnd: bgez $a1,inheritableSkillNumberForLoopStart

    inheritableSkillNumberOut: daddu $a0, $s4, $zero
    jal 0x00176A30
    nop
    daddu $a0, $zero, $v0 // We return total number of skills result persona has
    daddu $v0, $zero, $s1 // We return inheritable skills linked list info
    daddu $v1, $zero, $s0 // We return number of skills we can inherit

    ld $ra,0x90($sp)
    ld $fp,0x80($sp)
    ld $s7,0x70($sp)
    ld $s6,0x60($sp)
    ld $s5,0x50($sp)
    ld $s4,0x40($sp)
    ld $s3,0x30($sp)
    ld $s2,0x20($sp)
    ld $s1,0x10($sp)
    ld $s0,0x0($sp)
    addiu $sp,$sp,0xB0
    jr $ra
ASM_END


ASM_START 0x003D7978
    nop
    nop
    nop
    nop
    nop
    nop
    nop
    addiu $a1, $zero, 0xfff // We insert 0xfff into every inherited skill position
    // This will bug the view and inherited skills will show up blank, perfect for this mod to replace later

ASM_END